\chapter{Appendix} % (fold)
\label{cha:appendix}
\section{Lexer specificatie} % (fold)
\label{sec:lexer_specificatie}
Voor de lexer zijn de verschillende tokens van belang. Deze tokens staan hieronder allen gedefinieerd. \\ 

Tekens
\begin{description}
    \item \itab{Token}      \tab{teken}
    \item \itab{COLON}      \tab{:}
    \item \itab{SEMICOLON}  \tab{;}
    \item \itab{LPAREN}     \tab{(} 
    \item \itab{RPAREN}     \tab{)} 
    \item \itab{LBLOCK}     \tab{[}
    \item \itab{RBLOCK}     \tab{]}
    \item \itab{LCURLY}     \tab{\{}
    \item \itab{RCURLY}     \tab{\}}
    \item \itab{COMMA}      \tab{,}
    \item \itab{DOUBLE\_QUOTE} \tab{"}
    \item \itab{SINGLE\_QUOTE} \tab{\textbackslash}
    \item \itab{BODY}       \tab{body}
    \item \itab{EXPR}       \tab{assignment\_expression}
    \item \itab{GET}        \tab{get\_expression}
\end{description}

\clearpage

Operators
\begin{description}
    \item \itab{Token}      \tab{teken}
    \item \itab{PLUS}       \tab{+}
    \item \itab{MINUS}      \tab{-}
    \item \itab{DIVIDES}    \tab{/}
    \item \itab{MULTIPL}    \tab{*}
    \item \itab{POWER}      \tab{\^}
    \item \itab{LT}         \tab{\textless}
    \item \itab{GT}         \tab{\textgreater}
    \item \itab{GTE}        \tab{\textgreater =}
    \item \itab{LTE}        \tab{\textless =}
    \item \itab{EQ}         \tab{=}
    \item \itab{NEQ}        \tab{!}
    \item \itab{ASSIGN}     \tab{==}
    \item \itab{OR}         \tab{\textbar \textbar}
    \item \itab{AND}        \tab{\&\&}
\end{description}

Pointers
\begin{description}
    \item \itab{Token}      \tab{teken}
    \item \itab{AMPERSAND}  \tab{\&}
    \item \itab{ASTERIX}    \tab{\%}
\end{description}

\clearpage

Keywords van KIDEB
\begin{description}
    \item \itab{Token}      \tab{keyword}
    \item \itab{PROGRAM}    \tab{program}
    \item \itab{SWAP}       \tab{swap}
    \item \itab{IF}         \tab{if}
    \item \itab{THEN}       \tab{then}
    \item \itab{ELSE}       \tab{else}
    \item \itab{DO}         \tab{do}
    \item \itab{WHILE}      \tab{while}
    \item \itab{FROM}       \tab{from}
    \item \itab{IMPORT}     \tab{import}
    \item \itab{BREAK}      \tab{break}
    \item \itab{CONTINUE}   \tab{continue}
    \item \itab{RETURN}     \tab{return}
    \item \itab{FOR}        \tab{for}
    \item \itab{IN}         \tab{in}
    \item \itab{RETURNS}    \tab{returns}
    \item \itab{FUNC}       \tab{func}
    \item \itab{ARRAY}      \tab{array}
    \item \itab{ARGS}       \tab{args}
    \item \itab{CALL}       \tab{call}
    \item \itab{VAR}        \tab{var}
    \item \itab{OF}         \tab{of}
    \item \itab{PRINT}      \tab{print}
    \item \itab{TAM}        \tab{\_\_tam\_\_}
\end{description}

\clearpage

Standaard types
\begin{description}
    \item \itab{Token}      \tab{keyword}
    \item \itab{INTEGER}    \tab{int}
    \item \itab{CHARACTER}  \tab{char}
    \item \itab{BOOLEAN}    \tab{bool}
    \item \itab{AUTO}       \tab{auto}
\end{description}
% section lexer_specificatie (end)

\clearpage
\begin{landscape}
\section{Parser specificatie} % (fold)
\label{sec:parser_specificatie}
\begin{lstlisting}
// Parser rules
program: command+ -> ^(PROGRAM command+);

command:
    (IDENTIFIER ASSIGN) => assign_statement SEMICOLON! |
    (IDENTIFIER MULT* ASSIGN) => assign_statement SEMICOLON! |
    (IDENTIFIER LBLOCK expression RBLOCK ASSIGN) => assign_statement SEMICOLON! |
    statement |
    declaration |
    expression SEMICOLON!|
    SEMICOLON!;

commands: command commands?;

// Declarations
declaration:
    var_declaration |
    scope_declaration;

var_declaration: type IDENTIFIER (a=var_assignment)? SEMICOLON
                     -> {a == null}? ^(VAR type IDENTIFIER<IdentifierNode>)
                     -> ^(VAR type IDENTIFIER<IdentifierNode>) ^(ASSIGN IDENTIFIER<IdentifierNode> ^(EXPR $a));

var_assignment: ASSIGN! expression;

scope_declaration: 
    func_declaration;

func_declaration: FUNC IDENTIFIER LPAREN arguments? RPAREN (RETURNS t=type)? LCURLY commands? RCURLY
                      -> {t == null}? ^(FUNC IDENTIFIER<FunctionNode> IDENTIFIER<IdentifierNode>["auto"] ^(ARGS arguments?) ^(BODY commands?))
                      -> ^(FUNC IDENTIFIER<FunctionNode> type ^(ARGS arguments?) ^(BODY commands?));


// Parses arguments of function declaration
argument: type IDENTIFIER;
arguments: argument (COMMA! arguments)?;

// Statements
statement:
    if_statement | 
    while_statement |
    return_statement |
    for_statement |
    print_statement |
    import_statement |
    BREAK SEMICOLON! |
    CONTINUE SEMICOLON!;


if_part: IF LPAREN expression RPAREN LCURLY commands? RCURLY
             -> expression ^(THEN commands?);

else_part: ELSE LCURLY commands? RCURLY
             -> ^(ELSE commands?);

if_statement: if_part ep=else_part?
    -> ^(IF if_part else_part?);

while_statement: WHILE LPAREN expression RPAREN LCURLY command* RCURLY
                     -> ^(WHILE expression command*);
for_statement: FOR IDENTIFIER IN expression LCURLY commands? RCURLY
                   -> ^(FOR IDENTIFIER<IdentifierNode> expression commands?);

return_statement: RETURN expression SEMICOLON -> ^(RETURN expression);

import_statement
@init { CommonNode includetree = null; }:
  IMPORT s=STRING_VALUE {
    try {
      String filename = $s.text.substring(1, $s.text.length() - 1);
      GrammarLexer lexer = new GrammarLexer(new ANTLRFileStream(filename + ".kib"));
      GrammarParser parser = new GrammarParser(new CommonTokenStream(lexer));
      parser.setTreeAdaptor(new CommonNodeAdaptor());
      includetree = (CommonNode)(parser.program().getTree());
    } catch (Exception fnf) {
        // TODO: Error handling?
      ;
    }
  }
  -> ^({includetree})
;

assign:
    MULT assign -> ^(DEREFERENCE assign) |
    ASSIGN expression -> ^(EXPR expression) |
    LBLOCK expression RBLOCK a=assign -> ^(GET assign expression);

assign_statement: 
    IDENTIFIER assign -> ^(ASSIGN IDENTIFIER assign);

print_statement: PRINT LPAREN expression RPAREN -> ^(PRINT expression);

// Expressions, order of operands:
// ()
// ^
// *, /
// +, -
// <=, >=, <, >, ==, !=, ||, &&
expression:
    expressionAO |
    raw_expression |
    array_literal;

expressionAO: expressionLO (AND<TypedNode>^ expressionLO | OR<TypedNode>^ expressionLO)*;
expressionLO: expressionPM ((LT<TypedNode>^ | GT<TypedNode>^ | LTE<TypedNode>^ | GTE<TypedNode>^ | EQ<TypedNode>^ | NEQ<TypedNode>^) expressionPM)*;
expressionPM: expressionMD ((PLUS<TypedNode>^ | MINUS<TypedNode>^) expressionMD)*;
expressionMD: expressionPW ((MULT<TypedNode>^ | DIVIDES<TypedNode>^ | MOD<TypedNode>^) expressionPW)*;
expressionPW: operand (POWER<TypedNode>^ operand)*;

expression_list: expression (COMMA! expression_list)?;
raw_expression: TAM^ LPAREN! type COMMA! STRING_VALUE RPAREN!;
call_expression: IDENTIFIER LPAREN expression_list? RPAREN
                     -> ^(CALL IDENTIFIER expression_list?);

get_expression: IDENTIFIER LBLOCK expression RBLOCK
                    -> ^(GET IDENTIFIER expression);

operand:
    (IDENTIFIER LBLOCK) => get_expression|
    (IDENTIFIER LPAREN) => call_expression|
    (MULT IDENTIFIER) => DEREFERENCE^ IDENTIFIER<IdentifierNode> |
    NOT^ operand |
    AMPERSAND^ IDENTIFIER<IdentifierNode> |
    MULT operand -> ^(DEREFERENCE operand) |
    LPAREN! expression RPAREN! |
    IDENTIFIER<IdentifierNode> |
    NUMBER<TypedNode> |
    STRING_VALUE<TypedNode>|
    bool;

bool: TRUE<TypedNode> | FALSE<TypedNode>;

array_literal: LBLOCK array_value_list? RBLOCK -> ^(ARRAY array_value_list?);
array_value_list: expression (COMMA! array_value_list)?;

// Types
type:
    (primitive_type LBLOCK) => composite_type |
    MULT type -> ^(DEREFERENCE type) |
    primitive_type;

primitive_type:
    INTEGER<TypedNode> |
    BOOLEAN<TypedNode> |
    CHARACTER<TypedNode> |
    AUTO<TypedNode> |
    VAR<TypedNode>;

composite_type:
    primitive_type LBLOCK expression RBLOCK
        -> ^(ARRAY primitive_type expression+);

// Lexer rules
IDENTIFIER: (LETTER | UNDERSCORE) (LETTER | DIGIT | UNDERSCORE)*;
STRING_VALUE:  '\'' ( '\\' '\''? | ~('\\' | '\'') )* '\'';

NUMBER: DIGIT+;
COMMENT: '//' .* '\n' { $channel=HIDDEN; };
WS: (' ' | '\t' | '\f' | '\r' | '\n')+ { $channel=HIDDEN; };

fragment DIGIT : ('0'..'9');
fragment LOWER : ('a'..'z');
fragment UPPER : ('A'..'Z');
fragment UNDERSCORE : '_';
fragment LETTER : LOWER | UPPER;
\end{lstlisting}
% section parser_specificatie (end)
\section{Treeparser specificaties} % (fold)
\label{sec:treeparser_specificaties}

De Checker-klasse.
\begin{lstlisting}
tree grammar GrammarChecker;

options {
    tokenVocab=Grammar;
    ASTLabelType=CommonNode;
    output=AST;
}

@rulecatch {
    catch(RecognitionException e){
        throw e;
    }
}

@header {
    package checker;
    import java.util.Stack;
    import java.util.EmptyStackException;
    import symtab.SymbolTable;
    import symtab.SymbolTableException;
    import symtab.IdEntry;
    import ast.*;
    import reporter.Reporter;
    import org.javatuples.Pair;
}

@members {
    protected SymbolTable<IdEntry> symtab = new SymbolTable<>();

    private IdentifierNode getID(CommonNode node, String id) throws InvalidTypeException{
        if (symtab.retrieve(id) == null){
            reporter.error(node, "Could not find symbol.");
        }
        return symtab.retrieve(id).getNode();
    }

    private Checkers checkers = new Checkers(this);

    private Type assignType;

    private int argumentCount;

    private FunctionNode calling;

    private Stack<Pair<FunctionNode, Stack<CommonNode>>> loops = new Stack<Pair<FunctionNode, Stack<CommonNode>>>();

    private Stack<TypedNode> arrays = new Stack<>();

    public Reporter reporter;
    public void setReporter(Reporter r){ this.reporter = r; }
    public void log(String msg){ this.reporter.log(msg); }


}

program
@init {
    symtab.openScope();
}
@after {
    symtab.closeScope();
    loops.pop();
}
: ^(p=PROGRAM<FunctionNode>{
    loops.push(Pair.with((FunctionNode)$p.tree, new Stack<CommonNode>()));
    loops.peek().getValue0().setName("__root__");
    loops.peek().getValue0().setMemAddr(Pair.with(loops.size() - 1, -1));
} command+);

commands: command commands?;
command: declaration | expression | statement | ^(PROGRAM command+);

declaration: var_declaration | scope_declaration;

var_declaration:
    ^(VAR t=type id=IDENTIFIER<IdentifierNode>){
        IdentifierNode var = (IdentifierNode)$id.tree;

        try {
            symtab.enter($id.text, new IdEntry(var));
        } catch (SymbolTableException e) {
            reporter.error($id.tree, String.format(
                "but variable \%s was already declared \%s",
                $id.text, reporter.pointer(symtab.retrieve($id.text).getNode())
            ));
        }

        var.setExprType(((TypedNode)$t.tree).getExprType());

        if (var.getExprType().containsVariableType()){
            reporter.error($id.tree, "Variable cannot have variable type.");
        }

        // Register variable with function
        FunctionNode func = loops.peek().getValue0();
        func.getVars().add(var);
        var.setMemAddr(Pair.with(loops.size() - 1, func.getVars().size() - 1));

        log(String.format(
            "Set relative memory address of \%s to (\%s, \%s)",
            $id.text, var.getMemAddr().getValue0(), var.getMemAddr().getValue1()
        ));

        var.setScope(func);
        log(String.format("Setting scope of \%s to \%s().", $id.text, func.getName()));
    };

scope_declaration: func_declaration;

func_declaration:
    ^(FUNC id=IDENTIFIER<FunctionNode> t=type{
        FunctionNode func = (FunctionNode)$id.tree;
        func.setName($id.text);
        func.setScope(loops.peek().getValue0());
        func.setExprType(Type.Primitive.FUNCTION);
        log(String.format("Setting \%s.parent = \%s", $id.text, func.getScope().getName()));

        loops.push(Pair.with(func, new Stack<CommonNode>()));

        try {
            symtab.enter($id.text, new IdEntry((IdentifierNode)$id.tree));
        } catch (SymbolTableException e) {
            reporter.error($id.tree, String.format(
                "but variable \%s was already declared \%s",
                $id.text, reporter.pointer(symtab.retrieve($id.text).getNode())
            ));
        }

        func.setReturnType(((TypedNode)$t.tree).getExprType());
        symtab.openScope();
        func.setMemAddr(Pair.with(loops.size() - 1, -1));

        if (loops.size() > 6 + 2){ // +2 for root node, and current function declaration
            reporter.error(func, "You can only nest functions 6 levels deep, it's morally wrong to nest deeper ;-).");
        }

        argumentCount = 0;
    } ^(ARGS arguments?) {
        IdentifierNode arg;
        int inverse_count;
        for (int i=0; i < func.getArgs().size(); i++){
            arg = func.getArgs().get(i);
            inverse_count = -1 * (func.getArgs().size() - i);
            arg.setMemAddr(Pair.with(loops.size() - 1, inverse_count));

            log(String.format(
                "Setting relative address of \%s to (\%s, \%s).",
                arg.getText(), loops.size() - 1, inverse_count
            ));
        }
    } ^(BODY commands?)) {
        symtab.closeScope();
        loops.pop();
   };

argument: t=type id=IDENTIFIER<IdentifierNode>{
    IdentifierNode inode = (IdentifierNode)$id.tree;

    try {
        symtab.enter($id.text, new IdEntry(inode));
        ((TypedNode)$id.tree).setExprType(((TypedNode)$t.tree).getExprType());
    } catch (SymbolTableException e) {
        reporter.error(inode, e.getMessage());
    }

    FunctionNode function = loops.peek().getValue0();
    function.getArgs().add(inode);

    log(String.format(
        "Register argument \%s of \%s to \%s().",
        $id.text, ((TypedNode)$id.tree).getExprType(), function.getName()
    ));

    inode.setMemAddr(Pair.with(loops.size() - 1, 0));
};

arguments: argument {
    argumentCount += 1;
} arguments?;

statement:
    ^(PRINT expression) |
    ^(IF exp=expression {
        symtab.openScope();

        TypedNode ext = (TypedNode)$exp.tree;
        if(!(ext.getExprType().equals(Type.Primitive.BOOLEAN))){
            reporter.error($exp.tree, "Expression must of be of type boolean. Found: " + ext.getExprType() + ".");
        }
    } ^(THEN commands?) {
        symtab.closeScope();
        symtab.openScope();
    } (^(ELSE commands?))?){
        symtab.closeScope();
    } |
    ^(w=WHILE{
        loops.peek().getValue1().push($w.tree);
     } ex=expression command*) {
        checkers.type((TypedNode)$ex.tree, Type.Primitive.BOOLEAN);
    } |
    ^(r=RETURN<ControlNode> ex=expression){
        ControlNode ret = (ControlNode)$r.tree;
        ret.setScope(loops.peek().getValue0());

        FunctionNode func = (FunctionNode)ret.getScope();
        TypedNode expr = (TypedNode)$ex.tree;

        if(loops.size() == 1){
            reporter.error(r, "Return must be used in function.");
        }

        if (func.getReturnType().getPrimType() == Type.Primitive.AUTO){
            func.setReturnType(expr.getExprType());
            log(String.format("Setting '\%s' to \%s", func.getName(), func.getReturnType()));
        }

        checkers.typeEQ(expr, Type.Primitive.AUTO, "Return value must have type, not auto (maybe we did not discover it's type yet?)");

        // Test equivalence of types
        if (!func.getReturnType().equals(expr.getExprType(), true)){
            reporter.error(ret, String.format(
                "Expected \%s, but got \%s.", func.getReturnType(), expr.getExprType())
            );
        }
    }|
    b=BREAK<ControlNode>{
        try{
            CommonNode loop = loops.peek().getValue1().peek();
        } catch(EmptyStackException e){
            reporter.error($b.tree, "'break' outside loop.");
        }

        ((ControlNode)$b.tree).setScope(loops.peek().getValue0());
    }|
    c=CONTINUE<ControlNode>{
        try{
            CommonNode loop = loops.peek().getValue1().peek();
        } catch(EmptyStackException e){
            reporter.error($c.tree, "'continue' outside loop.");
        }

        ((ControlNode)$c.tree).setScope(loops.peek().getValue0());
    }|
    assignment;

assign:
    ^(a=DEREFERENCE<TypedNode> as=assign){
        ((TypedNode)$a.tree).setExprType(new Type(
            Type.Primitive.POINTER, ((TypedNode)$as.tree).getExprType()
        ));
    } |
    ^(expr=EXPR<TypedNode> ex=expression){
        ((TypedNode)$expr.tree).setExprType(((TypedNode)$ex.tree).getExprType());
    }|
    ^(g=GET<TypedNode> value=assign index=expression){
        assignType = assignType.getInnerType();


        ((TypedNode)$g.tree).setExprType(((TypedNode)$value.tree).getExprType());
    };


assignment: ^(a=ASSIGN id=IDENTIFIER<IdentifierNode>{
    IdentifierNode inode = (IdentifierNode)$id.tree;

    inode.setRealNode(getID(inode, $id.text));

    assignType = inode.getExprType();

} ex=assign{
    // If `id` is AUTO, infer type from expression
    if((inode.getExprType().getPrimType().equals(Type.Primitive.AUTO))){
        inode.setExprType((TypedNode)$ex.tree);
        log(String.format("Setting '\%s' to \%s", $id.text, inode.getExprType()));
    } 

    TypedNode ext = (TypedNode)$ex.tree;
    if(!assignType.equals(ext.getExprType(), true)){
        reporter.error($a.tree, String.format(
            "Cannot assign value of \%s to variable of \%s.",
            ext.getExprType(), assignType
        ));
    }
});

bool_op: AND | OR;
same_op: PLUS | MINUS | DIVIDES | MULT | MOD;
same_bool_op: EQ | NEQ;
same_bool_int_op: LT | GT | LTE | GTE;

expression_list: expr=expression {
    TypedNode arg = calling.getArgs().get(argumentCount);
    TypedNode exp = (TypedNode)$expr.tree;

    if (!arg.getExprType().equals(exp.getExprType(), true)){
        reporter.error(exp, String.format(
            "Argument \%s of \%s expected \%s, but got \%s.",
            argumentCount + 1, calling.getName(),
            arg.getExprType(), exp.getExprType()
        ));
    }

    argumentCount += 1;
} expression_list?;

expression:
    operand |
    ^(c=CALL<TypedNode> id=IDENTIFIER<IdentifierNode>{
        IdentifierNode idNode = (IdentifierNode)$id.tree;
        idNode.setRealNode(getID($id.tree, $id.text));
        FunctionNode func = calling = (FunctionNode)idNode.getRealNode();
        ((TypedNode)$c.tree).setExprType(func.getReturnType());

        argumentCount = 0;
    } expression_list? {
        if (argumentCount != func.getArgs().size()){
            reporter.error(func, String.format(
                "Expected \%s arguments, \%s given.", func.getArgs().size(), argumentCount
            ));
        }
    })|
    ^(op=bool_op ex1=expression ex2=expression) {
        ((TypedNode)$op.tree).setExprType(new Type(Type.Primitive.BOOLEAN));
        checkers.type((TypedNode)$ex1.tree, Type.Primitive.BOOLEAN);
        checkers.type((TypedNode)$ex2.tree, Type.Primitive.BOOLEAN);
    } |
    ^(op=same_op ex1=expression ex2=expression){
        TypedNode ext1 = (TypedNode)$ex1.tree;

        if(ext1.getExprType().getPrimType() == Type.Primitive.POINTER){
            log("Warning: pointer arithmetic is unchecked logic.");
            ((TypedNode)$op.tree).setExprType(ext1.getExprType());
        } else {
            checkers.equal($op.tree, (TypedNode)$ex1.tree, (TypedNode)$ex2.tree);
        }
    }|
    ^(op=same_bool_op ex1=expression ex2=expression){
        checkers.equal($op.tree, (TypedNode)$ex1.tree, (TypedNode)$ex2.tree);
        ((TypedNode)$op.tree).setExprType(Type.Primitive.BOOLEAN);
    }|
    ^(op=same_bool_int_op ex1=expression ex2=expression){
        checkers.type((TypedNode)$ex1.tree, Type.Primitive.INTEGER);
        checkers.type((TypedNode)$ex2.tree, Type.Primitive.INTEGER);
        ((TypedNode)$op.tree).setExprType(Type.Primitive.BOOLEAN);
    }|
    ^(tam=TAM<TypedNode> t=type STRING_VALUE){
        ((TypedNode)$tam.tree).setExprType(((TypedNode)$t.tree).getExprType());
    }|
    ^(p=DEREFERENCE<TypedNode> ex=expression){
        checkers.type((TypedNode)$ex.tree, Type.Primitive.POINTER, "Cannot dereference non-pointer.");

        // Dereference variable: take over inner type
        ((TypedNode)$p.tree).setExprType(((TypedNode)$ex.tree).getExprType().getInnerType());
    }|
    ^(p=AMPERSAND<TypedNode> id=IDENTIFIER<IdentifierNode>){
        // Make pointer to variable
        ((IdentifierNode)$id.tree).setRealNode(getID($id.tree, $id.text));

        ((TypedNode)$p.tree).setExprType(new Type(
            Type.Primitive.POINTER, ((IdentifierNode)$id.tree).getExprType()
        ));
    }|
    ^(get=GET<TypedNode> id=IDENTIFIER<IdentifierNode> ex=expression){
        IdentifierNode inode = (IdentifierNode)$id.tree;
        inode.setRealNode(getID($id.tree, $id.text));

        checkers.symbol(get, "get_from_array", "builtins/math");
        checkers.type(inode, Type.Primitive.ARRAY);
        checkers.type((TypedNode)$ex.tree, Type.Primitive.INTEGER);

        // Result returns inner type of array
        ((TypedNode)$get.tree).setExprType(inode.getExprType().getInnerType());
    }|
    ^(n=NOT<TypedNode> ex=expression){
        checkers.type((TypedNode)$ex.tree, Type.Primitive.BOOLEAN);
    }|
    ^(p=POWER<TypedNode> base=expression power=expression){
        checkers.equal((TypedNode)$p.tree, (TypedNode)$base.tree, (TypedNode)$power.tree);
        checkers.symbol((TypedNode)$p.tree, "power", "builtins/math");
    };

type:
    primitive_type |
    composite_type ;

primitive_type:
    i=INTEGER<TypedNode>    { ((TypedNode)$i.tree).setExprType(Type.Primitive.INTEGER); }|
    b=BOOLEAN<TypedNode>    { ((TypedNode)$b.tree).setExprType(Type.Primitive.BOOLEAN); }|
    c=CHARACTER<TypedNode>  { ((TypedNode)$c.tree).setExprType(Type.Primitive.CHARACTER); }|
    a=AUTO<TypedNode>       { ((TypedNode)$a.tree).setExprType(Type.Primitive.AUTO); }|
    v=VAR<TypedNode>        { ((TypedNode)$v.tree).setExprType(Type.Primitive.VARIABLE); };

composite_type:
    ^(arr=ARRAY<TypedNode> t=primitive_type size=expression){
        TypedNode sizen = (TypedNode)$size.tree;
        if(!sizen.getExprType().equals(Type.Primitive.INTEGER)){
            reporter.error($size.tree, "Expected Type<INTEGER> but found " + sizen.getExprType());
        }

        ((TypedNode)$arr.tree).setExprType(new Type(
            Type.Primitive.ARRAY, ((TypedNode)$t.tree).getExprType()
        ));

        checkers.symbol($size.tree, "alloc", "builtins/heap");
    }|
    ^(a=DEREFERENCE<TypedNode> t=type){
        ((TypedNode)$a.tree).setExprType(new Type(
            Type.Primitive.POINTER, ((TypedNode)$t.tree).getExprType()
        ));
    };

array_expression:
    ex=expression{
        Type arrType = arrays.peek().getExprType();
        Type expType = ((TypedNode)$ex.tree).getExprType();

        if(arrType.getInnerType().getPrimType() == Type.Primitive.AUTO){
            // We are the first one!
            arrType.setInnerType(expType);
        }

        if(arrType.getInnerType().getPrimType() == Type.Primitive.AUTO){
            // If this type is *still* AUTO, we do not know what to do.
            reporter.error($ex.tree, String.format(
                "Cannot assign AUTO types to an array of AUTO."
            ));
        }

        // Checking type against previous array element (essentially)
        if (!arrType.getInnerType().equals(expType)){
            reporter.error($ex.tree, String.format(
                "Elements of array must be of same type. Found: \%s, expected \%s.",
                expType, arrType.getInnerType()
            ));
        }
    };

operand:
    id=IDENTIFIER<IdentifierNode> {
        ((IdentifierNode)$id.tree).setRealNode(getID($id.tree, $id.text));
    } |
    n=NUMBER {
        ((TypedNode)$n.tree).setExprType(Type.Primitive.INTEGER);
    } |
    s=STRING_VALUE {
        ((TypedNode)$s.tree).setExprType(Type.Primitive.CHARACTER);
    } |
    b=(TRUE|FALSE){
        ((TypedNode)$b.tree).setExprType(Type.Primitive.BOOLEAN);
    } |
    ^(arr=ARRAY<TypedNode> {
        TypedNode array = (TypedNode)$arr.tree;
        array.setExprType(Type.Primitive.ARRAY);
        array.getExprType().setInnerType(new Type(Type.Primitive.AUTO));
        arrays.push(array);
    } values=array_expression*){
        arrays.pop();
    } 
;
\end{lstlisting}

De Codegenerator
\includecode[java]{De codegenerator}{../src/checker/GrammarChecker.g}
% section treeparser_specificaties (end)

\clearpage

\section{Testverslag} % (fold)
\label{sec:testverslag}
Dit zijn de python bestanden die alle tests afhandelen. De output bij incorrect broncode wordt afgevangen in de tests zelf en wordt dus niet als output gegeven. Er is alleen noemenswaardige output als de test faalt, dus er niet opgeleverd wordt wat verwacht is. Met de huidige test komt dit niet voor.

De syntax-tester.
\includecode[python]{De syntax-tester}{../tests/grammar.py}
De context-tester.
\includecode[python]{De context-tester}{../tests/checker.py}

% section testverslag (end)

\end{landscape}
% chapter appendix (end)